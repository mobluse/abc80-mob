10 REM FRA CREATIVE COMPUTING
20 REM 101 BASIC COMPUTER GAMES
30 REM SIDE 157-163
40 REM ----------------------
50 DIM G%(7%,7%),Q%(9%,9%),K%(3%,3%),D(8%),A$=22%,B$=10%,C$=6%
60 ; CHR$(12%)"(1) DITT FÖRSTA UPPDRAG" : ; "(2) DU HAR VARIT MED FÖRR" : ; "(3) DU ÄR SPECIALIST"
70 ONERRORGOTO 60 : ; : ; "ANGE 1,2 ELLER 3"; : INPUT I% : IF I%=1% E0%=5000% : T2%=25% : P0%=20%
80 IF I%<1% OR I%>3% 60 ELSE IF I%=2% E0%=3000% : T2%=10% : P0%=10%
90 IF I%=3% E0%=3000% : T2%=1% : P0%=10%
100 RANDOMIZE : E%=E0% : P%=P0% : T0%=(RND*20%+20%)*100% : T=T0% : T9%=25%+RND*10% : S%=0% : B9%=0% : K9%=0%
110 FOR I%=0% TO 8% : D(I%)=0% : NEXT I%
120 DEFFNB(B)=SQR((K%(I%,1%)-S1%)Ü2%+(K%(I%,2%)-S2%)Ü2%)
130 DEFFNA(C)=(C-1%)*45%
140 Q1%=RND*8% : Q2%=RND*8% : S1%=RND*10% : S2%=RND*10%
150 FOR I%=0% TO 7% : FOR J%=0% TO 7% : K3%=0% : B3%=0% : IF RND>.96 K3%=30% : K9%=K9%+3% : GOTO 180
160 IF RND>.95 K3%=20% : B3%=1% : K9%=K9%+2% : B9%=B9%+1% : GOTO 180
170 IF RND>.8 K3%=10% : K9%=K9%+1%
180 G%(I%,J%)=K3%+B3% : NEXT J% : NEXT I% : IF K9%+T2%>T9% T9%=K9%+T2%
190 ; CHR$(12%);"DINA ORDER:"
200 ; "STOPPA DE";K9% : ; "FIENDESKEPP INNAN DE ANGRIPER" : ; "HUVUDKVARTERET STJÄRNDATUM";T0%+T9%
210 ; "DETTA GER DIG"T9%;" STJÄRNDAGAR" : ; "DU HAR"B9%;" BASER TILL" : ; "UNDERHÅLL AV DITT SKEPP"
220 FOR I%=1% TO 3% : FOR J%=1% TO 3% : K%(I%,J%)=0% : NEXT J% : NEXT I%
230 K3%=INT(G%(Q1%,Q2%)/10%) : B3%=G%(Q1%,Q2%)-10%*K3% : S3%=RND*8%
240 FOR I%=0% TO 9% : FOR J%=0% TO 9% : Q%(I%,J%)=0% : NEXT J% : NEXT I%
250 Q%(S1%,S2%)=69% : IF K3%=0% 280
260 FOR I%=1% TO K3% : GOSUB 1420 : Q%(R1%,R2%)=70% : K%(I%,1%)=R1% : K%(I%,2%)=R2% : K%(I%,3%)=50%+(.5*RND*100%)
270 NEXT I%
280 IF B3%>0% GOSUB 1420 : Q%(R1%,R2%)=66%
290 FOR I%=1% TO S3% : GOSUB 1420 : Q%(R1%,R2%)=42% : NEXT I%
300 ; CUR(9%,0%)"-------------------" : ; CUR(20%,0%)"-------------------"
310 ; CUR(11%,21%)"KVADRAT"CUR(12%,21%)"SEKTOR"CUR(13%,21%)"DATUM"
320 ; CUR(10%,21%)"STATUS"CUR(15%,21%)"TORPEDER"CUR(16%,21%)"ENERGI"CUR(17%,21%)"SKÖLD"
330 ; CUR(18%,21%)"FIENDE"CUR(14%,21%)"DAGAR KVAR"CUR(19%,21%)"KOMMANDO" : GOSUB 1670
340 GOSUB 1890 : IF E%=0% AND D(6%)<0% 1460
350 ONERRORGOTO 370 : B%=0% : IF T>T0%+T9% 1450 ELSE B$="193205" : GOSUB 1480 : INPUT Ö% : IF Ö%<1% OR Ö%>7% 370
360 ON Ö% GOTO 410,830,890,1060,770,1270,1200
370 OUT 6%,0%
380 GOSUB 1540 : ; "KOMMANDO:" : ; "(1) KURS" : ; "(2) RADAR" : ; "(3) LASER" : ; "(4) TORPED"
390 ; "(5) ÄNDRA SKÖLD" : ; "(6) SKADERAPPORT" : ; "(7) DATAMASKIN" : GOTO 350
400 GOSUB 1890 : GOTO 340
410 GOSUB 1530 : ; "KURS (1-9)"; : INPUT C : IF C<1% OR C>9% GOSUB 1530 : ; "FEL KURS" : GOTO 410
420 GOSUB 1530 : W2=8% : IF D(0%)<0% ; "MOTORN ÄR SKADAD,MAX FART 0.2" : W2=.2
430 ; "FART (0.1-8.0)"; : INPUT W1 : IF W1<.1 GOSUB 1530 : ; "FÖR LÅG FART" : GOTO 430
440 IF W1>W2 GOSUB 1530 : ; "FÖR HÖG FART" : GOTO 430
450 N%=W1*8%+.5 : IF E%-N%<0% GOSUB 1530 : ; "ENERGIN RÄCKER INTE FÖR MANÖVERN" : GOSUB 1510 : GOTO 340
460 GOSUB 1530 : GOSUB 1540 : GOSUB 1310 : GOTO 520
470 D6=W1 : IF W1>1% D6=1%
480 FOR I%=0% TO 7% : IF D(I%)=0% 510
490 D(I%)=D(I%)+D6 : IF D(I%)<0% 510
500 D(I%)=0% : GOSUB 1530 : A%=I% : GOSUB 1560 : ; A$;":REP. UTFÖRT" : GOSUB 1510
510 NEXT I% : IF G%=1% 340 ELSE 220
520 S4%=S1% : S5%=S2% : Q4%=Q1% : Q5%=Q2% : W3=0% : Q%(S1%,S2%)=0%
530 U4%=S4% : U5%=S5% : GOSUB 1500 : W3=W3+.1 : GOSUB 1840
540 S4%=S1%+Z% : S5%=S2%+Y% : ; CUR(12%,32%)S4%;S5% : IF S4%<0% OR S4%>9% OR S5%<0% OR S5%>9% 640
550 IF Q%(S4%,S5%)<>0% 570 ELSE ; CUR(10%+S4%,2%*S5%)"E"
560 IF W3<W1 530 ELSE S1%=S4% : S2%=S5% : F%=0% : GOTO 600
570 IF Q%(S4%,S5%)=66% F%=1%
580 IF Q%(S4%,S5%)=42% F%=2%
590 W3=.1 : GOSUB 1840 : S1%=S4%-Z% : S2%=S5%-Y% : ; CUR(10%+S1%,2%*S2%)"E"
600 G%=1% : Q%(S1%,S2%)=69%
610 GOSUB 1860 : IF W1>1% T=T+1% ELSE T=T+W1
620 GOSUB 1670 : IF T>T0%+T9% 1450
630 GOTO 740
640 W3=W1 : GOSUB 1840
650 Q1%=10%*Q1%+S1%+Z% : Q2%=10%*Q2%+S2%+Y% : F%=0%
660 IF Q1%<0% Q1%=0% : F%=3%
670 IF Q1%>79% Q1%=79% : F%=3%
680 S1%=Q1%-INT(Q1%/10%)*10% : Q1%=INT(Q1%/10%)
690 IF Q2%<0% Q2%=0% : F%=3%
700 IF Q2%>79% Q2%=79% : F%=3%
710 S2%=Q2%-INT(Q2%/10%)*10% : Q2%=INT(Q2%/10%) : GOSUB 1670
720 IF Q1%=Q4% AND Q2%=Q5% IF F%=3% 590 ELSE 600
730 G%=0% : GOTO 610
740 FOR I%=1% TO 3% : IF K%(I%,3%)=0% 760
750 Q%(K%(I%,1%),K%(I%,2%))=0% : GOSUB 1420 : Q%(R1%,R2%)=70% : K%(I%,1%)=R1% : K%(I%,2%)=R2%
760 NEXT I% : GOTO 470
770 T=T+.1 : IF D(6%)<0% A%=6% : GOSUB 1550 : GOTO 340
780 GOSUB 1530 : ; "HUR MYCKET ENERGI TILL SKÖLDEN"; : INPUT X% : IF X%>E% 810
790 IF X%<0% 820
800 E%=E%-X% : S%=S%+X% : GOSUB 1670 : GOTO 350
810 GOSUB 1530 : ; CHR$(7%)CUR(21%,0%)"FÖR MYCKET" : GOTO 780
820 IF ABS(X%)>S% 810 ELSE 800
830 IF D(2%)<0% A%=2% : GOSUB 1550 : GOTO 340
840 GOSUB 1540 : ; "RADARSVEP FRÅN KVADRAT:";Q1%+1%;Q2%+1%
850 FOR I%=Q1%-1% TO Q1%+1% : IF I%<0% OR I%>7% ; " ■■  ■■  ■■"; : GOTO 880
860 FOR J%=Q2%-1% TO Q2%+1% : IF J%<0% OR J%>7% ; " ■■ "; : GOTO 880
870 ; G%(I%,J%)" "; : IF G%(I%,J%)>=0% AND G%(I%,J%)<=9% ; " ";
880 NEXT J% : ; : ; : NEXT I% : GOTO 340
890 T=T+.1 : IF D(3%)<0% A%=3% : GOSUB 1550 : GOTO 340
900 GOSUB 1530 : IF K3%<=0% ; CHR$(7%)"INGET FIENDE SKEPP I KVADRATEN" : GOSUB 1510 : GOTO 350
910 ; "LASERN LÅST PÅ MÅLET"
920 ; "HUR MYCKET ENERGI TILL LASERN"; : INPUT X% : IF X%>E% GOSUB 1530 : ; "FÖR MYCKET" : GOTO 920
930 GOSUB 1310 : GOSUB 1530 : E%=E%-X% : GOSUB 1670 : GOSUB 1540 : H1=X%/K3%
940 FOR I%=1% TO 3% : OUT 6%,7% : ; CUR(5%,0%); : A6%=198% : IF K%(I%,3%)=0% 1040
950 S5%=K%(I%,2%)
960 S4%=K%(I%,1%)
970 H=INT(H1/FNB(1)) : GOSUB 1650 : IF H<.15*K%(I%,3%) ; "INGEN SKADA PÅ FIENDE.SEKTOR";K%(I%,1%);K%(I%,2%)
980 REM ■■■■■■ DUMSKALLE -> GOTO 1120
990 K%(I%,3%)=K%(I%,3%)-H : ; TAB(4%)H;" ENHETER TRÄFFADE, SEKTOR";K%(I%,1%);K%(I%,2%) : OUT 6%,33%
1000 IF K%(I%,3%)>0% ; TAB(4%)"SENSOR VISER";K%(I%,3%);" ENHETER KVAR" : GOTO 1050
1010 ; TAB(5%)"■■ FIENDE SKEPPET FÖRINTAT ■■" : OUT 6%,9% : K3%=K3%-1% : K9%=K9%-1% : Q%(K%(I%,1%),K%(I%,2%))=0%
1020 K%(I%,3%)=0% : GOSUB 1670 : G%(Q1%,Q2%)=G%(Q1%,Q2%)-10% : IF K9%=0% GOSUB 1890 : GOSUB 1510 : GOTO 1430
1030 GOSUB 1510 : A6%=32% : GOSUB 1650
1040 B$="050079" : GOSUB 1480 : OUT 6%,0% : NEXT I% : GOSUB 1530 : GOTO 340
1050 GOSUB 1510 : IF A8%=1% 1030 ELSE A6%=70% : GOSUB 1650 : GOTO 1040
1060 T=T+.1 : IF P%=0% GOSUB 1530 : ; "TORPEDERNA SLUT" : GOSUB 1510 : GOTO 340
1070 IF D(4%)<0% A%=4% : GOSUB 1550 : GOTO 340
1080 GOSUB 1530 : ; "TORPED KURS"; : INPUT C : IF C<1% OR C>8.99 GOSUB 1530 : ; "FEL KURS" : GOTO 1080
1090 GOSUB 1310 : T=T+.1 : P%=P%-1% : GOSUB 1670 : S4%=S1% : S5%=S2% : W3=0%
1100 W3=W3+.1 : GOSUB 1840 : S4%=S1%+Z% : S5%=S2%+Y% : IF S4%<0% OR S4%>9% OR S5%<0% OR S5%>9% 1190
1110 L%=Q%(S4%,S5%) : IF L%=0% 1100
1120 IF L%=66% GOSUB 1530 : OUT 6%,45% : ; "STJÄRNBASEN FÖRINTAD" : A6%=194% : B9%=B9%-1% : L%=1% : GOTO 1170
1130 IF L%=42% GOSUB 1530 : OUT 6%,9% : ; "DU TRÄFFADE EN STJÄRNA" : A6%=170% : GOTO 1180
1140 GOSUB 1540 : OUT 6%,91% : ; TAB(5%)"■■FIENDE SKEPPET FÖRINTAT■■" : A6%=198% : K3%=K3%-1% : K9%=K9%-1% : L%=10%
1150 GOSUB 1670 : IF K9%=0% GOSUB 1890 : GOSUB 1510 : GOTO 1430
1160 FOR I%=1% TO 3% : IF S4%=K%(I%,1%) AND S5%=K%(I%,2%) K%(I%,3%)=0% ELSE NEXT I%
1170 Q%(S4%,S5%)=0% : G%(Q1%,Q2%)=G%(Q1%,Q2%)-L%
1180 GOSUB 1650 : GOSUB 1510 : GOTO 340
1190 GOSUB 1530 : ; "TORPEDEN MISSADE"CHR$(7%) : GOTO 350
1200 T=T+.1 : GOSUB 1540 : IF D(7%)<0% A%=7% : GOSUB 1550 : GOTO 340
1210 FOR I%=0% TO 9% : FOR J%=0% TO 9% : IF Q%(I%,J%)=66% 1220 ELSE NEXT J% : NEXT I% : GOTO 1230
1220 A=S1%-I% : B=(9%-S2%)-(9%+J%) : GOSUB 1780 : ; "RIKTNING TILL BASEN";V
1230 I%=0% : IF K3%=0% 900
1240 FOR I%=1% TO 3% : IF K%(I%,3%)=0% 1260 ELSE A=S1%-K%(I%,1%) : B=(9%-S2%)-(9%-K%(I%,2%)) : GOSUB 1780
1250 ; "RIKTNING TILL FIENDESKEPP";V
1260 NEXT I% : GOTO 350
1270 T=T+.1 : IF D(5%)<0% A%=5% : GOSUB 1550 : GOTO 340
1280 GOSUB 1540 : FOR I%=0% TO 7% : IF I%=5% 1300 ELSE ON I%+1% GOSUB 1570,1580,1590,1600,1610,1620,1630,1640
1290 ; A$;TAB(22%)D(I%)
1300 NEXT I% : GOTO 350
1310 IF K3%<0% RETURN
1320 IF F%=1% GOSUB 1530 : ; "BASEN SKYDDAR DIG" : GOSUB 1510 : RETURN
1330 FOR I%=1% TO 3% : GOSUB 1540 : IF K%(I%,3%)<=0% 1410
1340 H=INT((K%(I%,3%)/FNB(1))*(2%+RND)) : IF H<1% 1410 : S%=S%-H : K%(I%,3%)=K%(I%,3%)/(2%+RND)
1350 IF K%(I%,3%)<=1% K%(I%,3%)=1%
1360 ; CUR(5%,0%)"TRÄFF PÅ DITT SKEPP. SKÖLD NED MED";H
1370 FOR K%=S%+H TO S% STEP -5% : ; CUR(17%,31%)"      "CUR(17%,31%)K%;CHR$(7%) : FOR J%=1% TO 999% : NEXT J%
1380 ; CUR(17%,32%)S% : NEXT K% : FOR K%=1% TO 3000% : NEXT K% : IF S%<0% 1440
1390 S=S% : IF H/S<.02 OR RND>.75 THEN 1410
1400 A%=RND*8% : D(A%)=D(A%)-H/S-.5*RND : GOSUB 1560 : ; CUR(7%,0%)"SKADA PÅ ";A$ : GOSUB 1510
1410 NEXT I% : RETURN
1420 R1%=RND*10% : R2%=RND*10% : IF Q%(R1%,R2%)=0% RETURN ELSE 1420
1430 GOSUB 1540 : OUT 6%,45% : ; "GRATULERAR KAPTEN!" : ; "DET SISTA FIENDESKEPPET ÄR ÖDELAGT" : GOTO 1470
1440 GOSUB 1510 : GOSUB 1540 : OUT 6%,9% : ; "DITT SKEPP ÄR ÖDELAGT" : GOTO 1470
1450 GOSUB 1510 : GOSUB 1540 : OUT 6%,3% : ; "TIDEN FÖR DITT UPPDRAG ÄR UTE" : GOTO 1470
1460 GOSUB 1540 : OUT 6%,3% : ; "DU ÄR AVSATT!!" : ; "SKEPPET KAN INTE MANÖVRERAS"
1470 GOSUB 1510 : OUT 6%,0% : FOR X%=1% TO 10000% : NEXT X% : GOTO 50
1480 A1%=VAL(LEFT$(B$,2%)) : A2%=VAL(MID$(B$,3%,2%)) : A3%=VAL(RIGHT$(B$,5%))
1490 ; CUR(A1%,A2%)STRING$(A3%,32%) : ; CUR(A1%,A2%); : RETURN
1500 B$=RIGHT$(NUM$(10%+U4%),2%)+RIGHT$(NUM$(100%+U5%*2%),3%)+"01" : GOSUB 1480 : RETURN
1510 FOR K%=1% TO 10000% : NEXT K% : RETURN
1520 FOR K%=0% TO 9% : B$=NUM$(10%+K%)+"0020" : B$=RIGHT$(B$,2%) : GOSUB 1480 : NEXT K% : RETURN
1530 B$="210079" : GOSUB 1480 : RETURN
1540 B$="0000320" : GOSUB 1480 : RETURN
1550 GOSUB 1530 : GOSUB 1560 : ; A$;" FUNGERAR INTE" : GOSUB 1510 : RETURN
1560 ; CHR$(7%) : ON A%+1% GOTO 1570,1580,1590,1600,1610,1620,1630,1640
1570 A$="MOTOR" : RETURN
1580 A$="KORT-DISTANS SENSOR" : RETURN
1590 A$="LÅNG-DISTANS SENSOR" : RETURN
1600 A$="LASERKONTROLL" : RETURN
1610 A$="TORPEDKONTROLL" : RETURN
1620 A$="SKADEKONTROLL" : RETURN
1630 A$="SKÖLDKONTROLL" : RETURN
1640 A$="DATAMASKIN" : RETURN
1650 P1%=32040% : IF S4%>=6% P1%=31056%
1660 POKE P1%+S4%*128%+S5%*2%,A6% : RETURN
1670 FOR K%=1% TO 8% : ; CUR(10%+K%,32%)"        "CUR(10%+K%,32%);
1680 ON K% GOTO 1690,1700,1710,1720,1730,1740,1750,1760
1690 ; Q1%+1%;Q2%+1% : GOTO 1770
1700 ; S1%;S2% : GOTO 1770
1710 ; T : GOTO 1770
1720 ; T0%+T9%-T : GOTO 1770
1730 ; P% : GOTO 1770
1740 ; E% : GOTO 1770
1750 ; S% : GOTO 1770
1760 ; K9%
1770 NEXT K% : RETURN
1780 ONERRORGOTO 1820 : V=ATN(A/B)*180%/PI
1790 IF B<0% V=180%+V ELSE IF A<0% AND B>0% V=360%+V
1800 V=1%+V/45% : IF V=9% V=1%
1810 RETURN
1820 V=0% : IF B=0% AND A>0% V=90% ELSE IF B=0% AND A<0% V=270%
1830 GOTO 1790
1840 Z%=10%*(INT(10%*(SIN(FNA(C+180%)*PI/180%)*W3)+.5)/10%)
1850 Y%=10%*(INT(10%*(COS(FNA(C)*PI/180%)*W3)+.5)/10%) : RETURN
1860 E%=E%-N%-10% : IF E%>=0% RETURN
1870 ; CUR(22%,0%)"ENERGI FRA SKJOLD" : S%=S%+E% : E%=0% : GOSUB 1510 : IF S%<0% S%=0%
1880 RETURN
1890 IF K3%>0% C$="■RÖD■" : OUT 6%,7% : GOTO 1910
1900 OUT 6%,0% : C$="GRÖN" : IF E%<E0%*.1 C$="-GUL-"
1910 IF F%=1% C$="DOCKAD" : E%=E0% : P%=P0% : S%=0% : GOSUB 1670
1920 B$="103207" : GOSUB 1480 : ; C$
1930 A8%=0% : IF D(1%)<0% A%=1% : A8%=1% : GOSUB 1520 : GOSUB 1550
1940 IF D(1%)<0% RETURN
1950 ; CUR(10%,0%); : FOR K%=0% TO 9% : FOR J%=0% TO 9% : ; CHR$(Q%(K%,J%));" "; : NEXT J% : ; : NEXT K%
1960 IF F%=0% RETURN ELSE IF NOT Ö%=1% RETURN ELSE GOSUB 1530 : ; CHR$(7%); : ON F% GOTO 1970,1980,1990
1970 ; "DOCKNING MED RYMDBASEN" : RETURN
1980 ; "KOLLISION MED EN STJÄRNA" : RETURN
1990 ; "MOTORN AVSTÄNGD PÅ GRÄNSEN" : ; "TILL DEN YTTRE RYMDEN" : RETURN
